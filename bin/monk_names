#!/usr/bin/env perl
use warnings;
use strict;
use Act::Config;
use Act::User;
use LWP::Simple;
use DBI;

use Getopt::Std;
our %opts;
getopts("nv", \%opts) or usage();

# init the database handle
$Request{dbh} = DBI->connect(
    $Config->database_dsn, $Config->database_user,
    $Config->database_passwd, { AutoCommit => 0 }
  )
  or die "can't connect to database: " . $DBI::errstr;

# fetch all users
my @users = grep { $_->monk_id } @{ Act::User->get_items() };

print "Updating monk ids...\n" if @users && $opts{v};
for my $u (@users) {
    my $page = get("http://perlmonks.org/index.pl?node_id=$u->{monk_id}") || "";
    my ($name) = $page =~ m!<title>([^<]*)</title>!;

    if ($name && (!$u->monk_name || $u->monk_name ne $name)) {
        if ($opts{v}) {
            print $u->monk_id, "\t";
            print $u->monk_name, ' => ' if $u->monk_name;
            print $name, "\n";
        }
        $u->update( monk_name => $name ) unless $opts{n};
    }
}
sub usage
{
    die <<EOF;
Usage: $0 [-nv]

-n   dry run
-v   verbose
EOF
}
