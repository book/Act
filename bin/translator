#!/usr/bin/env perl
use strict;
use warnings;

# useful modules
use File::Spec::Functions;
use File::Slurp;
use Term::ReadKey;
use Tie::IxHash;
use Getopt::Long;

my %conf = (
    acthome => catfile( $ENV{ACTHOME} || '.' ),
);
GetOptions( \%conf, 'rev=i', 'acthome=s' ) or die usage();

my $podir = catfile( $conf{acthome}, 'po' );
my $posvn = catfile( $conf{acthome}, 'po', '.svn', 'text-base' );

my $lang = shift;

# special cases
# --rev <rev> option
if ( $conf{rev} ) {
    my $lex = read_po( $lang  => catfile( $podir, "$lang.po" ) );
    my $rev = read_rev( $lang => catfile( $podir, "$lang.rev" ) );
    $rev->{$_} = $conf{rev} for keys %$lex;
    write_rev( $lang, $rev );
    exit;
}
# the case for 'en'
if ( $lang eq 'en' ) {
    my $ref_lex = read_po( 'en'  => catfile( $posvn, 'en.po.svn-base' ) );
    my $ref_rev = read_rev( 'en' => catfile( $posvn, 'en.rev.svn-base ' ) );
    my $new_lex = read_po( 'en'  => catfile( $podir, 'en.po' ) );
    my $new_rev = read_rev( 'en' => catfile( $podir, 'en.rev' ) );

    # find new / modified ids
    $new_rev->{$_} = ( $ref_rev->{$_} || 0 ) + 1
        for grep { ( $new_lex->{$_} ||= '' ) ne ( $ref_lex->{$_} ||= '' ) }
        keys %$new_lex;

    # find deleted ids
    delete $new_rev->{$_} 
        for grep { not exists $new_lex->{$_} }
        keys %$ref_lex;

    # rewrite rev file
    write_rev( $lang, $new_rev );
    exit;
}

# load both lexicons and revisions
my $ref_lex = read_po( 'en'   => catfile( $podir, 'en.po' ) );
my $ref_rev = read_rev( 'en'  => catfile( $podir, 'en.rev' ) );
my $ext_lex = read_po( $lang  => catfile( $podir, "$lang.po" ) );
my $ext_rev = read_rev( $lang => catfile( $podir, "$lang.rev" ) );

# various interactive actions
my %action = (
    a => sub {
        my $id = shift;
        $ext_rev->{$id} = $ref_rev->{$id};
    },
    e => sub {
        my $id = shift;
        $ext_lex->{$id} = get_new_text();
        $ext_rev->{$id} = $ref_rev->{$id};
    },
    i => sub { },
    x => sub { exit; },
);

# find the outdated ids
my @todo = grep { ( $ext_rev->{$_} ||= 0 ) < ( $ref_rev->{$_} ||= 1 ) }
    keys %$ref_lex;

# process the ids interactively
my $answer = 'a';
for my $id (@todo) {
    $ext_lex->{$id} = '' if ! defined $ext_lex->{$id};
    print
        "$id seems outdated (en\@$ref_rev->{$id}, $lang\@$ext_rev->{$id})\n",
        "- en: $ref_lex->{$id}\n", "- $lang: $ext_lex->{$id}\n";
    $answer = get_choice($answer);
    last if $answer eq 'q';

    # execute the action
    $action{$answer}->($id);
}

write_po( $lang, $ext_lex );
write_rev( $lang, $ext_rev );

exit;

#
# USEFUL SUBS
#
sub read_po {
    my ( $lang, $file ) = @_;

    my %lexicon;
    tie %lexicon, 'Tie::IxHash';

    return \%lexicon if ! -e $file;
    open my $fh, '<', $file or die "Can't open $file: $!";
    my ($msgid, $msgstr);
    while (<$fh>) {
        chomp;
        if (/^msgid "(.*)"$/) {
            $msgid = $1;
            $msgstr = '';
        }
        elsif (/^msgstr "(.*)"$/) {
            $msgstr = $1;
        }
        elsif (/^"(.*)"/) {
            $msgstr .= $1;
        }
        elsif (/^ *$/) {
            if ($msgid) {
                for ($msgstr) {
                    s/\\n/\n/g;
                    s/\\//g;
                }
                $lexicon{$msgid} = $msgstr ;
                undef $msgid;
            }
        }
        else {
            die "$file: syntax error line $.\n";
        }
    }
    $lexicon{$msgid} = $msgstr if $msgid;
    return \%lexicon;
}

sub write_po {
    my ( $lang, $lex ) = @_;
    my $file = catfile( $podir, "$lang.po" );

    open my $fh, '>', $file or die "Can't open $file: $!";
    print $fh <<EOF;
msgid ""
msgstr ""
"MIME-Version: 1.0\\n"
"Content-Type: text/plain; charset=UTF-8\\n"
"Content-Transfer-Encoding: 8bit\\n"

EOF
    for my $id ( keys %$lex ) {
        # msgid
        print $fh qq(msgid "$id"\n);

        # msgstr
        ( my $str = $lex->{$id} ) =~ s/"/\\"/g;
        if ( $str =~ /\n/ ) {
            $str = join "\n", '""', map {qq("$_\\n")} split /\n/, $str;
        }
        else { $str = qq("$str"); }
        print $fh qq(msgstr $str\n\n);
    }

    close $fh;
}

sub read_rev {
    my ( $lang, $file ) = @_;

    my %rev;
    tie %rev, 'Tie::IxHash';

    if (-e $file) {
        %rev = map { chomp; split / \@ / } read_file $file;
    }
    return \%rev;
}

sub write_rev {
    my ( $lang, $rev ) = @_;
    my $file = catfile( $podir, "$lang.rev" );

    write_file( $file,
        map {"$_ \@ $rev->{$_}\n"} grep { !/^__|^$/ } keys %$rev );
}

sub get_choice {
    my $default = shift;
    ( my $replies = "aeiqx" ) =~ s/$default/uc( $default )/e;
    my $key = '';
    print "Accept, Edit, Ignore, Quit, eXit? [$replies]: ";
    ReadMode 4;    # Turn off controls keys
    while ( $key !~ /^[aeiqx\n]+$/i ) {
        1 while not defined( $key = ReadKey(0) );
    }
    ReadMode 0;    # Reset tty mode before exiting
    print "$key\n";
    $key = $default if $key eq "\n";
    return lc $key;
}

sub get_new_text {

}

sub usage {
    return << 'USAGE';
Usage: translator [ --rev <rev> ] <lang>
USAGE
}

